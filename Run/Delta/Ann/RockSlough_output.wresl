! R.Wilbur & S. Wu 5/10/05

!****************************************************************************************

! maximum export given the Sacramento River flow

define RS_MaxExport {lower unbounded upper unbounded kind 'FLOW' units 'CFS'}
goal setRSMax {
  lhs RS_MaxExport
  case LowCL{
    condition C_SAC041_ANN(-1) >=40000.
    rhs 999999.}
    case NoSalinityControlPossible{
    condition abs(RS_m_1_sv) <= 0.001 .and. int(RS_b_1_sv) == 0
    rhs 999999.}
  case NegSlope{
    condition RS_m_1_sv <= 0.0
    rhs 999999.}
  case LargeNegInt{
      condition RS_b_1_sv < -10000.0
    rhs 999999.}
  case Normal {
    condition always
    rhs RS_m_1_sv*C_SACtot_ANN + RS_b_1_sv}
}


!****************************************************************************************

! required delta outflow given exports

define RS_MRDO {lower unbounded upper unbounded kind 'FLOW' units 'CFS'}
goal setRSmrdo1 {
  lhs RS_MRDO
  case LowCL{
    condition C_SAC041_ANN(-1) >=40000.
    rhs 0.}
  case NoSalinityControlPossible{
    condition abs(RS_m_1_sv) <= 0.001 .and. int(RS_b_1_sv) == 0
    rhs 0.}
  case NegSlope{
    condition RS_m_1_sv <= 0.0
    rhs 0.}
  case LargeNegInt{
    condition RS_b_1_sv < -10000.0
    rhs 0.}
  case Normal {
    condition always
    rhs ((1-RS_m_1_sv)/RS_m_1_sv)*ExportActualTD  - RS_b_1_sv/RS_m_1_sv + DeltaInflowforNDOI - C_SACTOT_ANN - DeltaExportforNDOI + C_CAA003_TD + C_DMC000_TD
						   + DeltaAccretionforNDOI - DeltaDepletionforNDOI}
}


!****************************************************************************************

! required sac flow given exports

define RS_ReqSac{lower unbounded upper unbounded kind 'FLOW' units 'cfs'}
goal set_RS_ReqSac {
  lhs RS_ReqSac
  case LowCL{
    condition C_SAC041_ANN(-1) >=40000.
    rhs 0.}
  case NoSalinityControlPossible{
    condition abs(RS_m_1_sv) <= 0.001 .and. int(RS_b_1_sv) == 0
    rhs 0.}
  case NegSlope{
    condition RS_m_1_sv <= 0.0
    rhs 0.}
  case LargeNegInt{
      condition RS_b_1_sv < -10000.0
    rhs 0.}
  case Normal {
    condition always
    rhs ExportActualTD/RS_m_1_sv - RS_b_1_sv/RS_m_1_sv}
}

